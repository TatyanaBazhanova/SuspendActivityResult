apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

group = 'dev.hichamboushaba.suspendactivityresult'
version = findProperty('LIBRARY_VERSION')

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId group
                artifactId PUBLISH_ARTIFACT_ID
                version version

                from components.release
                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_NAME
                    description = 'A lightweight library for converting Activity Results to coroutines'
                    url = 'https://github.com/hichamboushaba/SuspendActivityResult'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'hichamboushab'
                            name = 'Hicham Boushaba'
                            email = 'hicham.boushaba@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/hichamboushaba/SuspendActivityResult.git'
                        developerConnection = 'scm:git:ssh://github.com/hichamboushaba/SuspendActivityResult.git'
                        url = 'http://github.com/hichamboushaba/SuspendActivityResult'
                    }
                }
            }
        }
    }
}

def signingKeyId = findProperty('SIGNING_KEY_ID')
def signingKeyPwd = findProperty('SIGNING_PASSPHRASE')
def signingKey = findProperty('SIGNING_KEY')

signing {
    useInMemoryPgpKeys(signingKeyId, signingKey, signingKeyPwd)
    sign publishing.publications
}
